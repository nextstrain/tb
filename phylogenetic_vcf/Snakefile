"""
Perform phylogenetic analysis starting from a vcf file for tb
"""
rule all:
    input:
        auspice_json = "auspice/tb.json",

seq_file = "data/all.vcf.gz"
ref_file = "defaults/GCF_000195955.2_ASM19595v2_genomic.fna"
meta_file = "data/metadata.tsv"
exclude_file = "defaults/dropped_strains.txt"
mask_file = "defaults/Locus_to_exclude_Mtb.bed"
sites_file = "defaults/drm_sites.txt"
generef_file = "defaults/GCF_000195955.2_ASM19595v2_genomic.gff"
auspice_config_file = "defaults/auspice_config.json"
strain_id_field = "accession_sra"
# Note that two files were copied from the previous tb repo and
# may need to be modified in the future:
# defaults/Locus_to_exclude_Mtb.bed
# config/drm_sites.txt

rule filter:
    input:
        seq = seq_file,
        meta = meta_file,
        exclude = exclude_file
    output:
        "results/filtered.vcf.gz"
    params:
       strain_id = strain_id_field
    shell:
        """
        augur filter --sequences {input.seq} \
            --metadata {input.meta} \
            --metadata-id-columns {params.strain_id} \
            --exclude {input.exclude} \
            --output {output}
        """

rule mask:
    input:
        seq = "results/filtered.vcf.gz",
        mask = mask_file
    output:
       "results/masked.vcf.gz"
    shell:
        """
        augur mask --sequences {input.seq} \
            --mask {input.mask} \
            --output {output}
        """

rule tree:
    input:
        aln = "results/masked.vcf.gz",
        ref = ref_file,
        sites = sites_file
    output:
        "results/tree_raw.nwk"
    params:
        method = 'iqtree'
    shell:
        """
        augur tree --alignment {input.aln} \
            --vcf-reference {input.ref} \
            --method {params.method} \
            --exclude-sites {input.sites} \
            --output {output}
        """

rule refine:
    input:
        tree = "results/tree_raw.nwk",
        aln = "results/masked.vcf.gz",
        metadata = meta_file,
        ref = ref_file
    output:
        tree = "results/tree.nwk",
        node_data = "results/branch_lengths.json",
    params:
        root = 'mid_point',
        strain_id = strain_id_field,
        coal = 'opt'
    shell:
        """
        augur refine --tree {input.tree} \
            --alignment {input.aln} \
            --vcf-reference {input.ref} \
            --metadata {input.metadata} \
            --metadata-id-columns {params.strain_id} \
            --timetree \
            --coalescent {params.coal} \
            --root {params.root} \
            --output-tree {output.tree} \
            --output-node-data {output.node_data}
        """

rule ancestral:
    input:
        tree = "results/tree.nwk",
        alignment = "results/masked.vcf.gz",
        ref = ref_file
    output:
        nt_data = "results/nt_muts.json",
        vcf_out = "results/nt_muts.vcf"
    params:
        inference = "joint"
    shell:
        """
        augur ancestral --tree {input.tree} \
            --alignment {input.alignment} \
            --vcf-reference {input.ref} \
            --inference {params.inference} \
            --output-node-data {output.nt_data} \
            --output-vcf {output.vcf_out}
        """

rule translate:
    input:
        tree = "results/tree.nwk",
        ref = ref_file,
        gene_ref = generef_file,
        vcf = "results/nt_muts.vcf",
    output:
        aa_data = "results/aa_muts.json",
        vcf_out = "results/translations.vcf",
        vcf_ref = "results/translations_reference.fasta"
    shell:
        """
        augur translate --tree {input.tree} \
            --vcf-reference {input.ref} \
            --ancestral-sequences {input.vcf} \
            --reference-sequence {input.gene_ref} \
            --output-node-data {output.aa_data} \
            --alignment-output {output.vcf_out} \
            --vcf-reference-output {output.vcf_ref}
        """

# We are currently excluding the nucleotide and amino acid mutations
# from the auspice tree in order to reduce file size
rule export:
    input:
        tree = "results/tree.nwk",
        metadata = meta_file,
        branch_lengths = "results/branch_lengths.json",
        # nt_muts = "results/nt_muts.json",
        # aa_muts = "results/aa_muts.json",
        auspice_config = auspice_config_file,
    output:
        auspice_json = rules.all.input.auspice_json,
    params:
        strain_id = strain_id_field
    shell:
        """
        augur export v2 \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --metadata-id-columns {params.strain_id} \
            --node-data {input.branch_lengths}  \
            --auspice-config {input.auspice_config} \
            --output {output.auspice_json} \
        """
