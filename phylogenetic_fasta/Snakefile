"""
Perform phylogenetic analysis starting from a fasta alignment for tb
"""
rule all:
    input:
        auspice_json = "auspice/tb.json",

# names of files used in the analysis
seq_file = "data/clean.full.aln"
meta_file = "data/metadata.tsv"
exclude_file = "defaults/dropped_strains.txt"
mask_file = "defaults/Locus_to_exclude_Mtb.bed"
sites_file = "defaults/drm_sites.txt"
auspice_config_file = "defaults/auspice_config.json"
genbank_ref = "defaults/GCF_000195955.2_ASM19595v2_genomic.gbff"
strain_id_field = "accession_sra"

rule filter:
    input:
        seq = seq_file,
        meta = meta_file,
        exclude = exclude_file
    output:
        "results/filtered.aln"
    params:
        strain_id = strain_id_field
    shell:
        """
        augur filter --sequences {input.seq} \
            --metadata {input.meta} \
            --metadata-id-columns {params.strain_id} \
            --exclude {input.exclude} \
            --output {output}
        """

rule mask:
    input:
        seq = "results/filtered.aln",
        mask = mask_file
    output:
       "results/masked.fasta"
    shell:
        """
        augur mask --sequences {input.seq} \
            --mask {input.mask} \
            --output {output}
        """

rule tree:
    input:
        aln = "results/masked.fasta",
        sites = sites_file
    output:
        "results/tree_raw.nwk"
    params:
        method = 'iqtree'
    shell:
        """
        augur tree --alignment {input.aln} \
            --method {params.method} \
            --exclude-sites {input.sites} \
            --output {output}
        """

# Timetree analysis is excluded due to lack of strong time signal
rule refine:
    input:
        tree = "results/tree_raw.nwk",
        aln = "results/masked.fasta",
        metadata = meta_file,
    output:
        tree = "results/tree.nwk",
        node_data = "results/branch_lengths.json",
    params:
        root = 'mid_point',
        strain_id = strain_id_field
    shell:
        """
        augur refine --tree {input.tree} \
            --alignment {input.aln} \
            --metadata {input.metadata} \
            --metadata-id-columns {params.strain_id} \
            --root {params.root} \
            --output-tree {output.tree} \
            --output-node-data {output.node_data}
        """

rule ancestral:
    input:
        tree = "results/tree.nwk",
        alignment = "results/masked.fasta"
    output:
        nt_data = "results/nt_muts.json"
    params:
        inference = "joint"
    shell:
        """
        augur ancestral --tree {input.tree} \
            --alignment {input.alignment} \
            --inference {params.inference} \
            --keep-ambiguous \
            --output-node-data {output.nt_data}
        """

rule translate:
    input:
        tree = "results/tree.nwk",
        genbank_ref = genbank_ref,
        node_data = "results/nt_muts.json",
    output:
        aa_data = "results/aa_muts.json",
    shell:    
        """
        augur translate --tree {input.tree} \
            --ancestral-sequences {input.node_data} \
            --reference-sequence {input.genbank_ref} \
            --output {output.aa_data}
        """

# We are currently excluding the nucleotide and amino acid mutations
# from the auspice tree in order to reduce file size
rule export:
    input:
        tree = "results/tree.nwk",
        metadata = meta_file,
        branch_lengths = "results/branch_lengths.json",
        # nt_muts = "results/nt_muts.json",
        # aa_muts = "results/aa_muts.json",
        auspice_config = auspice_config_file,
    output:
        auspice_json = rules.all.input.auspice_json,
    params:
        strain_id = strain_id_field
    shell:
        """
        augur export v2 \
            --tree {input.tree} \
            --metadata {input.metadata} \
            --metadata-id-columns {params.strain_id} \
            --node-data {input.branch_lengths} \
            --auspice-config {input.auspice_config} \
            --output {output.auspice_json} \
        """
